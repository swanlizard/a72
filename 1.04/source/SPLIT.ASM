	MOV	SI,81H
	MOV	DI,OFFSET INFILE
	CALL	GETFN
	JZ	NOPE
	MOV	DI,OFFSET OUTFILE
	CALL	GETFN
	JZ	NOPE
	PUSH	SI
	MOV	SI,OFFSET OUTFILE+1
	CALL	RDWORD
	POP	SI
	MOV	[SPLIT],AX
	MOV	[SPLIT+2],DX
	MOV	DI,OFFSET OUTFILE
	CALL	GETFN
	JNZ	MAIN
	MOV	SI,OFFSET INFILE
	MOV	DI,OFFSET OUTFILE
	MOV	CX,40H
	CLD
	REPZ	MOVSB
	JMP	SHORT MAIN
NOPE:	MOV	AH,9
	MOV	DX,OFFSET USAGE
	INT	21H
	RET
MAIN:	XOR	AX,AX
	MOV	[PIECE],AX
	MOV	AX,3D00H
	MOV	DX,OFFSET INFILE+1
	INT	21H
	JC	MAIN2
	MOV	[INHDL],AX
MAIN1:	CALL	RUN
	JNC	MAIN1
	MOV	AH,3EH
	MOV	BX,[INHDL]
	INT	21H
MAIN2:	RET
RUN:	MOV	SI,OFFSET SPLIT
	MOV	DI,OFFSET LEFT
	MOV	CX,4
	CLD
	REPZ	MOVSB
	MOV	DI,OFFSET OUTFILE
	STC
	CALL	ADDEXT
	MOV	DI,OFFSET DOT+1
	MOV	AX,[PIECE]
	CALL	STR
	JC	RUN0
	MOV	DI,OFFSET OUTFILE
	MOV	SI,OFFSET DOT
	CLC
	CALL	ADDEXT
	MOV	AH,3CH
	XOR	CX,CX
	MOV	DX,OFFSET OUTFILE+1
	INT	21H
	JC	RUN0
	MOV	[OUTHDL],AX
	INC	WORD PTR [PIECE]
RUN1:	XOR	DX,DX
	MOV	CX,8192
	CMP	DX,[LEFT+2]
	JC	RUN2
	CMP	CX,[LEFT]
	JC	RUN2
	MOV	CX,[LEFT]
RUN2:	MOV	AH,3FH
	MOV	DX,OFFSET BUFFER
	MOV	BX,[INHDL]
	INT	21H
	TEST	AX,AX
	JZ	RUN3
	PUSH	AX
	MOV	CX,AX
	MOV	AH,40H
	MOV	DX,OFFSET BUFFER
	MOV	BX,[OUTHDL]
	INT	21H
	POP	AX
	XOR	DX,DX
	SUB	[LEFT],AX
	SBB	[LEFT+2],DX
	MOV	AX,[LEFT]
	ADD	AX,[LEFT+2]
	JNZ	RUN1
	MOV	AH,3EH
	MOV	BX,[OUTHDL]
	INT	21H
	CLC
	RET
RUN3:	MOV	AH,3EH
	MOV	BX,[OUTHDL]
	INT	21H
RUN0:	STC
	RET
STR:	CMP	AX,1000
	JNC	STR0
	MOV	BX,DI
	MOV	CX,10
STR1:	XOR	DX,DX
	DIV	CX
	ADD	DL,30H
	MOV	[DI],DL
	INC	DI
	TEST	AX,AX
	JNZ	STR1
	MOV	CX,DI
	SUB	CX,BX
	CMP	CX,3
	JNC	STR5
	NEG	CX
	ADD	CX,3
	MOV	AL,30H
	CLD
	REPZ	STOSB
STR5:	DEC	DI
	MOV	AL,[DI]
	XCHG	AL,[BX]
	STOSB
	CLC
	RET
STR0:	STC
	RET
GETFN:	XOR	CX,CX
	PUSH	DI
	INC	DI
	CLD
GETFN1:	LODSB
	CMP	AL,0DH
	JZ	GETFN0
	CMP	AL,21H
	JC	GETFN2
	CMP	AL,61H
	JC	GETFN3
	CMP	AL,7BH
	JNC	GETFN3
	AND	AL,0DFH
GETFN3:	INC	CX
	STOSB
	JMP	SHORT GETFN1
GETFN2:	TEST	CX,CX
	JZ	GETFN1
GETFN0:	DEC	SI
	MOV	[DI],CH
	POP	DI
	MOV	[DI],CL
	TEST	CX,CX
	RET
ADDEXT:	LAHF
	CLD
	MOV	BP,DI
	XOR	CH,CH
	MOV	CL,[DI]
	INC	DI
	MOV	AL,2EH
	REPNZ	SCASB
	JNZ	ADDXT2
	SAHF
	JNC	ADDXT0
	DEC	DI
ADDXT2:	SAHF
	JC	ADDXT1
	STOSB
	CALL	WRM
ADDXT1:	MOV	[DI],CH
	MOV	AX,DI
	SUB	AX,BP
	DEC	AX
	MOV	DI,BP
	STOSB
ADDXT0:	RET
RDWORD:	XOR	DX,DX
	XOR	BX,BX
	XOR	AH,AH
	CLD
RDWOR1:	LODSB
	SUB	AL,30H
	JC	RDWOR0
	CMP	AL,0AH
	JNC	RDWOR0
	CLC
	RCL	DX,1
	RCL	BX,1
	MOV	CX,DX
	MOV	BP,BX
	CLC
	RCL	DX,1
	RCL	BX,1
	CLC
	RCL	DX,1
	RCL	BX,1
	ADD	DX,CX
	ADC	BX,BP
	ADD	DX,AX
	ADC	BX,0
	JMP	SHORT RDWOR1
RDWOR0:	XCHG	AX,BX
	XCHG	AX,DX
	DEC	SI
	RET
WRM:	MOV	CL,[SI]
	XOR	CH,CH
	INC	SI
	CLD
	REPZ	MOVSB
	RET
USAGE	DB	"SPLIT <IN> <SIZE> "
	DB	"[<OUT>]",13,10,36
DOT	DB	3,"000"
SPLIT	DD	?
LEFT	DD	?
PIECE	DW	?
INHDL	DW	?
OUTHDL	DW	?
INFILE	DS	20H
OUTFILE	DS	20H
BUFFER:
