TEMP	EQU	80H
TEMP2	EQU	0C0H
	MOV	AH,9
	MOV	DX,OFFSET AMSG
	INT	21H
	MOV	AH,30H
	INT	21H
	TEST	AL,AL
	JZ	BADDOS
	MOV	AX,SP
	SUB	AX,OFFSET BUFFER+80H
	MOV	[MAXMEM],AX
	CALL	KILL
	MOV	SI,81H
	MOV	DI,OFFSET INFN+2
RFN:	LODSB
	CMP	AL,0DH
	JZ	RFN0
	CMP	AL,21H
	JC	RFN
RFN1:	STOSB
	LODSB
	CMP	AL,21H
	JNC	RFN1
RFN0:	XOR	AL,AL
	STOSB
	MOV	AX,DI
	SUB	AX,OFFSET INFN+3
	JZ	NOPE
	MOV	[INFN],AX
	MOV	AX,[INFN+2]
	CMP	AX,3F2FH
	JZ	SYNTAX
	CALL	ZERO
	CALL	FBUF
NOPE:	CALL	STAT
	MOV	AH,9
	MOV	DX,OFFSET FORH
	INT	21H
MAIN:	CALL	READ
	CALL	DISP
	JMP	SHORT MAIN
BADDOS:	MOV	AH,9
	MOV	DX,OFFSET DOSERR
	INT	21H
	INT	20H
SYNTAX:	MOV	AH,9
	MOV	DX,OFFSET USAGE
	INT	21H
QUIT:	MOV	AX,4C00H
	INT	21H
ZERO:	CLD
	XOR	AX,AX
	MOV	[INPUT],AX
	MOV	[TEMP],AX
	MOV	[TEMP2],AX
	MOV	CX,5
	MOV	DI,OFFSET CMDS
	REPZ	STOSW
	RET
KILL:	XOR	AX,AX
	MOV	DI,OFFSET BUFLEN
	MOV	CX,5
	CLD
	REPZ	STOSW
	MOV	[INFN],AX
	INC	AX
	MOV	DI,OFFSET CUR
	STOSW
	STOSW
	RET
READ:	CALL	ZERO
	MOV	AH,9
	MOV	DX,OFFSET PROMPT
	INT	21H
	CALL	INP
	MOV	SI,OFFSET INPUT+1
	MOV	DI,OFFSET CMDS
	INC	CX
READ1:	LODSB
	CMP	AL,0DH
	JZ	READ6
	CMP	AL,2CH
	JZ	READ8
	CMP	AL,30H
	JC	READ1
	CMP	AL,3AH
	JC	READ9
	CMP	AL,3FH
	JZ	READ0
	CMP	AL,41H
	JC	READ1
	CMP	AL,5BH
	JC	READ3
	CMP	AL,61H
	JC	READ1
	AND	AL,0DFH
READ3:	MOV	[CMDS+8],AL
	MOV	BX,OFFSET TEMP
	LEA	DI,[BX+2]
READ4:	LODSB
	CMP	AL,0DH
	JZ	READ7
	CMP	AL,1AH
	JZ	READ5
	STOSB
	JMP	SHORT READ4
READ5:	CALL	READ7
	MOV	BX,OFFSET TEMP2
	LEA	DI,[BX+2]
	JMP	SHORT READ4
READ6:	MOV	AX,[CMDS]
	TEST	AX,AX
	JZ	READ
	RET
READ7:	DEC	DI
	DEC	DI
	SUB	DI,BX
	MOV	AX,DI
	MOV	[BX],AX
	MOV	AL,[CMDS+8]
	RET
READ0:	MOV	[CMDS+9],AL
	JMP	SHORT READ1
READ8:	DEC	CX
	JNZ	READ1
	XOR	AX,AX
	STOSW
	INC	CX
	JMP	SHORT READ1
READ9:	DEC	SI
	PUSH	CX
	CALL	VAL
	POP	CX
	STOSW
	INC	CX
	JMP	SHORT READ1
YNCP:	MOV	AH,9
	MOV	DX,OFFSET YNC
	INT	21H
YNCP1:	XOR	AX,AX
	INT	16H
	AND	AL,0DFH
	CMP	AL,"Y"
	JZ	YNCP3
	CMP	AL,"N"
	JZ	YNCP2
	CMP	AL,"C"
	JNZ	YNCP1
	STC
	RET
YNCP3:	TEST	AL,AL
YNCP2:	CLC
	RET
VAL:	XOR	DX,DX
	XOR	AH,AH
	CLD
VAL1:	LODSB
	SUB	AL,30H
	JC	VAL0
	CMP	AL,0AH
	JNC	VAL0
	SHL	DX,1
	MOV	CX,DX
	SHL	DX,1
	SHL	DX,1
	ADD	DX,CX
	ADD	DX,AX
	JMP	SHORT VAL1
VAL0:	MOV	AX,DX
	DEC	SI
	RET
STR:	MOV	BX,DI
	MOV	CX,10
STR1:	XOR	DX,DX
	DIV	CX
	ADD	DL,30H
	MOV	[DI],DL
	INC	DI
	TEST	AX,AX
	JNZ	STR1
	MOV	CX,DI
	SUB	CX,BX
	SHR	CX,1
	JZ	STR0
	STD
	PUSH	DI
	DEC	DI
STR2:	MOV	AL,[DI]
	XCHG	[BX],AL
	STOSB
	INC	BX
	LOOP	STR2
	POP	DI
STR0:	RET
PREPF:	CLD
	MOV	AX,[TEMP]
	TEST	AX,AX
	JNZ	PREPF1
	STC
	RET
PREPF1:	MOV	DX,OFFSET TEMP+2
	MOV	SI,OFFSET TEMP
	MOV	DI,DX
	ADD	DI,AX
	XOR	AX,AX
	STOSW
	RET
FIND:	CLD
	MOV	AL,0DH
FIND1:	PUSH	CX
	MOV	CX,[TEMP]
	MOV	DI,OFFSET TEMP+2
	PUSH	SI
	REPZ	CMPSB
	POP	SI
	JZ	FIND2
	CMP	[SI],AL
	JNZ	FIND3
	INC	BX
FIND3:	INC	SI
	POP	CX
	LOOP	FIND1
	STC
	RET
FIND2:	POP	CX
	CLC
	RET
RESIZE:	LEA	DI,[BX+BUFFER]
	CMP	CX,DX
	JZ	RESIZ0
	PUSH	CX
	PUSH	SI
	PUSH	DI
	CLD
	MOV	SI,DI
	ADD	SI,DX
	ADD	DI,CX
	MOV	AX,[BUFLEN]
	SUB	AX,DX
	ADD	AX,CX
	CMP	[MAXMEM],AX
	JC	RESIZ0
	XCHG	AX,[BUFLEN]
	ADD	AX,OFFSET BUFFER
	MOV	CX,AX
	SUB	CX,SI
	CMP	SI,DI
	JA	RESIZ1
	ADD	SI,CX
	ADD	DI,CX
	STD
RESIZ1:	INC	CX
	REPZ	MOVSB
	POP	DI
	POP	SI
	POP	CX
	CLD
	CLC
RESIZ0:	RET
RANGE:	MOV	DI,OFFSET BUFFER
	MOV	CX,[BUFLEN]
	XOR	DX,DX
	MOV	SI,DI
	JCXZ	RANGE5
	MOV	AX,0A0DH
	CLD
	TEST	BX,BX
	JNZ	RANGE1
	INC	BX
RANGE1:	INC	DX
	MOV	SI,DI
	REPNZ	SCASB
	JCXZ	RANGE5
	CMP	[DI],AH
	JNZ	RANGE2
	INC	DI
	DEC	CX
	JZ	RANGE5
RANGE2:	CMP	DX,BX
	JC	RANGE1
RANGE3:	TEST	BP,BP
	JZ	RANGE4
	CMP	DX,BP
	JNC	RANGE5
RANGE4:	INC	DX
	REPNZ	SCASB
	JCXZ	RANGE5
	CMP	[DI],AH
	JNZ	RANGE3
	INC	DI
	LOOP	RANGE3
RANGE5:	MOV	AX,DI
	SUB	AX,OFFSET BUFFER
	XCHG	DX,DI
	MOV	BX,SI
	SUB	BX,OFFSET BUFFER
	SUB	DX,SI
	TEST	DI,DI
	RET
INP:	CLD
	MOV	AX,[TEMP]
	TEST	AX,AX
	JNZ	INP1
	DEC	AX
	MOV	[INPUT],AX
	MOV	AH,0AH
	MOV	DX,OFFSET INPUT
	INT	21H
	MOV	AH,9
	MOV	DX,OFFSET CRLF
	INT	21H
	MOV	AX,[INPUT+1]
	CMP	AX,1A01H
	JNZ	INP2
	STC
	RET
INP1:	MOV	SI,OFFSET TEMP+2
	MOV	DI,OFFSET INPUT+1
	MOV	CX,AX
	STOSB
	REPZ	MOVSB
INP2:	XOR	AH,AH
	MOV	SI,OFFSET INPUT+2
	MOV	BX,AX
	MOV	DI,SI
	ADD	BX,SI
INP3:	LODSB
	CMP	AL,0DH
	JZ	INP6
	CMP	AL,1AH
	JNZ	INP5
	STOSB
	CMP	SI,BX
	JNC	INP6
	LODSB
	CMP	AL,1AH
	JNZ	INP5
	MOV	AL,[SI]
	CMP	AL,1AH
	JNZ	INP4
	DEC	SI
	JMP	SHORT INP5
INP4:	CMP	SI,BX
	JNC	INP6
	LODSB
	CMP	AL,21H
	JC	INP4
	DEC	SI
	DEC	DI
	CALL	VAL
INP5:	STOSB
	CMP	SI,BX
	JC	INP3
INP6:	MOV	AX,0A0DH
	STOSW
	MOV	AX,DI
	SUB	AX,OFFSET INPUT+2
	MOV	[INPUT+1],AL
	CLC
	RET
ECHO:	PUSH	CX
	PUSH	SI
	PUSH	BX
	CALL	RANGE
	JZ	ECHO0
	POP	DI
	PUSH	DI
	XCHG	DI,DX
	LEA	SI,[BX+BUFFER]
	LEA	BP,[BX+DI+BUFFER]
	MOV	CX,DX
ECHO1:	MOV	DI,OFFSET INPUT
	MOV	AX,CX
	PUSH	CX
	CALL	STR
	CLD
	LEA	CX,[DI-INPUT-6]
	NEG	CX
	MOV	AX,3E2DH
	REPZ	STOSB
	POP	CX
	STOSW
	MOV	AX,924H
	STOSB
	MOV	DX,OFFSET INPUT
	INT	21H
	MOV	AH,2
ECHO2:	LODSB
	MOV	DL,AL
	INT	21H
	CMP	AL,0DH
	JZ	ECHO3
	CMP	SI,BP
	JC	ECHO2
	JMP	SHORT ECHO0
ECHO3:	MOV	AL,[SI]
	CMP	AL,0AH
	JNZ	ECHO4
	MOV	DL,AL
	INT	21H
	INC	SI
ECHO4:	INC	CX
	CMP	SI,BP
	JC	ECHO1
ECHO0:	POP	BX
	POP	SI
	POP	CX
	RET
LDEL:	MOV	BX,[CMDS]
	TEST	BX,BX
	JZ	LDEL0
	MOV	BP,[CMDS+2]
	TEST	BP,BP
	JNZ	LDEL1
	MOV	BP,BX
LDEL1:	CALL	RANGE
	XOR	CX,CX
	CALL	RESIZE
LDEL0:	STC
	RET
LMOV:	CALL	LCPY
	JNC	LMOV0
	MOV	BX,[CMDS]
	MOV	DX,[CMDS+2]
	XOR	CX,CX
	CALL	RESIZE
	STC
LMOV0:	RET
LCPY:	MOV	BX,[CMDS+2]
	TEST	BX,BX
	JZ	LCPY0
	MOV	BP,[CMDS+4]
	TEST	BP,BP
	JZ	LCPY0
	CALL	RANGE
	MOV	[CMDS+4],BX
	MOV	[CMDS+2],DX
	MOV	BX,[CMDS]
	TEST	BX,BX
	JZ	LCPY0
	XOR	BP,BP
	CALL	RANGE
	MOV	CX,[CMDS+2]
	XOR	DX,DX
	CALL	RESIZE
	JNC	LCPY1
	JMP	NOMEM
LCPY1:	LEA	DI,[BX+BUFFER]
	MOV	BX,[CMDS+4]
	LEA	SI,[BX+BUFFER]
	CLD
	REPZ	MOVSB
	STC
LCPY0:	RET
LREP:	CALL	SRCH
	JC	LREP0
LREP1:	CALL	FIND
	JC	LREP0
	MOV	AL,[CMDS+9]
	CMP	AL,3FH
	JNZ	LREP2
	MOV	BP,BX
	CALL	ECHO
	CALL	YNCP
	JC	LREP0
	JNZ	LREP2
	ADD	SI,[TEMP]
	SUB	CX,[TEMP]
	JA	LREP1
	STC
	RET
LREP2:	PUSH	CX
	MOV	DX,[TEMP]
	MOV	CX,[TEMP2]
	LEA	BX,[SI-BUFFER]
	CALL	RESIZE
	POP	CX
	JNC	LREP3
	JMP	NOMEM
LREP3:	MOV	DI,OFFSET TEMP2+2
	XCHG	SI,DI
	PUSH	CX
	MOV	CX,[TEMP2]
	REPZ	MOVSB
	XCHG	SI,DI
	POP	CX
	SUB	CX,[TEMP]
	JA	LREP1
LREP0:	STC
	RET
LFND:	CALL	SRCH
	JC	LFND0
LFND1:	CALL	FIND
	JC	LFND0
	MOV	BP,BX
	CALL	ECHO
	ADD	SI,[TEMP]
	SUB	CX,[TEMP]
	JA	LFND1
LFND0:	CLC
	RET
PEND:	PUSH	DX
	MOV	BX,[CMDS]
	MOV	BP,BX
	CALL	RANGE
	MOV	DX,[CMDS]
	TEST	DX,DX
	JNZ	PEND1
	MOV	BX,AX
PEND1:	POP	DX
	RET
SRCH:	MOV	BX,[CMDS]
	MOV	BP,[CMDS+2]
	TEST	BX,BX
	JNZ	SRCH1
	INC	BX
SRCH1:	PUSH	BX
	CALL	RANGE
	POP	BX
	JZ	SRCH0
	MOV	CX,DX
	CLC
	RET
SRCH0:	STC
	RET
LIST:	XOR	BX,BX
	XOR	BP,BP
	CALL	RANGE
	MOV	BX,[CMDS]
	MOV	BP,[CMDS+2]
	TEST	BX,BX
	JNZ	LIST1
	MOV	BX,[CUR]
	LEA	BP,[BX+22]
	JMP	SHORT LIST2
LIST1:	TEST	BP,BP
	JNZ	LIST2
	LEA	BP,[BX+22]
LIST2:	CMP	DI,BP
	JNC	LIST3
	MOV	BP,DI
LIST3:	MOV	[CUR],BP
	CALL	ECHO
	CLC
	RET
LEDT:	MOV	AH,9
	MOV	DX,OFFSET CTRLZ
	INT	21H
	MOV	BX,[CMDS]
	TEST	BX,BX
	JZ	LEDT0
	MOV	BP,BX
	CALL	ECHO
	CALL	INP
	JC	LEDT0
	PUSH	AX
	MOV	BX,[CMDS]
	MOV	BP,BX
	CALL	RANGE
	POP	BP
	MOV	CX,BP
	CALL	RESIZE
	JNC	LEDT1
	JMP	NOMEM
LEDT1:	MOV	SI,OFFSET INPUT+2
	MOV	CX,BP
	REPZ	MOVSB
	STC
LEDT0:	RET
LINS:	MOV	AH,9
	MOV	DX,OFFSET CTRLZ
	INT	21H
	CALL	PEND
	MOV	BP,BX
LINS1:	CALL	INP
	JC	LINS0
	XOR	DX,DX
	MOV	CX,AX
	MOV	BX,BP
	CALL	RESIZE
	JNC	LINS2
	JMP	NOMEM
LINS2:	ADD	BP,CX
	MOV	SI,OFFSET INPUT+2
	REPZ	MOVSB
	JMP	SHORT LINS1
LINS0:	STC
	RET
DISP:	MOV	DI,OFFSET CMDLTR
	MOV	CX,CMDOFF-CMDLTR
	CLD
	REPNZ	SCASB
	JNZ	DISP1
	MOV	BX,CMDOFF-CMDLTR-1
	SUB	BX,CX
	SHL	BX,1
	ADD	BX,OFFSET CMDOFF
	JMP	[BX]
DISP1:	MOV	AX,[CMDS]
	TEST	AX,AX
	JZ	DISP2
	JMP	LEDT
DISP2:	CLC
	RET
HELP:	MOV	AH,9
	MOV	DX,OFFSET HLPT
	INT	21H
	CLC
	RET
NOMEM:	MOV	AH,9
	MOV	DX,OFFSET MEMERR
	INT	21H
	CLC
	RET
WINP:	MOV	AX,0A0DH
	STOSW
	MOV	AX,924H
	STOSB
	MOV	DX,OFFSET INPUT
	INT	21H
	CLC
	RET
FAIL:	CLD
	PUSH	SI
	CALL	FBUF0
	MOV	SI,OFFSET FILERR
	MOV	DI,OFFSET INPUT
	MOV	CX,9
	REPZ	MOVSB
	POP	SI
	LODSW
	MOV	CX,AX
	REPZ	MOVSB
	JMP	WINP
FXPT:	CALL	PREPF
	JC	FXPT0
	MOV	AH,3CH
	XOR	CX,CX
	MOV	DX,OFFSET TEMP+2
	MOV	SI,OFFSET TEMP
	INT	21H
	JNC	FXPT1
	JMP	FAIL
FXPT1:	PUSH	AX
	MOV	BX,[CMDS]
	MOV	BP,[CMDS+2]
	CALL	RANGE
	MOV	AH,40H
	LEA	CX,[BX+BUFFER]
	XCHG	CX,DX
	POP	BX
	INT	21H
	MOV	AH,3EH
	INT	21H
	CLC
	RET
FXPT0:	MOV	AH,9
	MOV	DX,OFFSET NOFILE
	INT	21H
	CLC
	RET
STAT:	CLD
	MOV	SI,OFFSET INFN
	MOV	DI,OFFSET INPUT
	LODSW
	MOV	CX,AX
	TEST	AX,AX
	JNZ	STAT1
	MOV	SI,OFFSET BLANK
	MOV	CX,10
STAT1:	REPZ	MOVSB
	MOV	AX,202CH
	STOSW
	PUSH	DI
	XOR	BX,BX
	XOR	BP,BP
	CALL	RANGE
	POP	AX
	XCHG	AX,DI
	CALL	STR
	CLD
	MOV	SI,OFFSET LINES
	MOV	CX,6
	REPZ	MOVSB
	MOV	AX,[STLIN]
	CMP	AX,1
	JZ	STAT3
	MOV	CX,6
	REPZ	MOVSB
	CALL	STR
	CLD
STAT3:	MOV	AX,202CH
	STOSW
	MOV	AX,[MAXMEM]
	SUB	AX,[BUFLEN]
	CALL	STR
	CLD
	MOV	SI,OFFSET BYTES
	MOV	CX,11
	REPZ	MOVSB
	JMP	WINP
FSAV:	MOV	AX,3D02H
	MOV	DX,OFFSET INFN+2
	MOV	SI,OFFSET INFN
	INT	21H
	JNC	FSAV1
	JMP	FAIL
FSAV1:	MOV	[INFILE],AX
	MOV	AH,3CH
	MOV	DX,OFFSET TMPFN+2
	MOV	SI,OFFSET TMPFN
	XOR	CX,CX
	MOV	SI,DX
	INT	21H
	JNC	FSAV2
	MOV	AH,3EH
	MOV	BX,[INFILE]
	INT	21H
	JMP	FAIL
FSAV2:	MOV	[TMPFILE],AX
	MOV	AX,4200H
	MOV	CX,[ENDPOS+2]
	MOV	DX,[ENDPOS]
	MOV	BX,[INFILE]
	MOV	BP,[TMPFILE]
	INT	21H
	CALL	XFER
	MOV	AX,4200H
	MOV	CX,[STPOS+2]
	MOV	DX,[STPOS]
	INT	21H
	MOV	AH,40H
	MOV	CX,[BUFLEN]
	MOV	DX,OFFSET BUFFER
	INT	21H
	XCHG	BX,BP
	MOV	AX,4200H
	XOR	CX,CX
	XOR	DX,DX
	INT	21H
	CALL	XFER
	MOV	AX,[STPOS]
	MOV	DX,[STPOS+2]
	ADD	AX,[BUFLEN]
	ADC	DX,0
	MOV	[ENDPOS],AX
	MOV	[ENDPOS+2],DX
	MOV	AH,40H
	XOR	CX,CX
	INT	21H
	MOV	AH,3EH
	MOV	BX,[INFILE]
	INT	21H
	MOV	AH,3EH
	MOV	BX,[TMPFILE]
	INT	21H
	MOV	AH,41H
	MOV	DX,OFFSET TMPFN+2
	INT	21H
	CLC
	RET
XFER:	MOV	AH,3FH
	MOV	CX,100H
	MOV	DX,OFFSET INPUT
	INT	21H
	PUSH	AX
	MOV	CH,40H
	MOV	DX,OFFSET INPUT
	XCHG	AX,CX
	XCHG	BX,BP
	INT	21H
	XCHG	BX,BP
	POP	AX
	TEST	AX,AX
	JNZ	XFER
	RET
FBUF:	CALL	PREPF
	JC	FBUF1
	CLD
	MOV	DI,OFFSET INFN
	LODSW
	MOV	CX,AX
	STOSW
	REPZ	MOVSB
FBUF1:	MOV	SI,OFFSET INFN
	MOV	AX,[SI]
	TEST	AX,AX
	JZ	FBUF0
	MOV	DX,OFFSET INFN+2
	MOV	AX,3D02H
	INT	21H
	JNC	FBUF2
	JMP	FAIL
FBUF0:	MOV	AH,9
	MOV	DX,OFFSET ERROR
	INT	21H
	CLC
	RET
FBUF2:	PUSH	AX
	MOV	AX,[CMDS]
	XCHG	AX,[CMDS+2]
	MOV	[CMDS+4],AX
	XOR	AX,AX
	MOV	[BUFLEN],AX
	INC	AX
	MOV	[CUR],AX
	MOV	[CMDS],AX
	MOV	DI,[INFN]
	MOV	CX,DI
	MOV	AX,242EH
	LEA	DI,[DI+INFN+1]
	STD
	REPNZ	SCASB
	JZ	FBUF3
	MOV	CX,[INFN]
FBUF3:	MOV	SI,OFFSET INFN+2
	MOV	DI,OFFSET TMPFN+2
	CLD
	REPZ	MOVSB
	MOV	AX,242EH
	STOSW
	MOV	AL,AH
	STOSW
	LEA	AX,[DI-TMPFN+2]
	MOV	[TMPFN],AX
	POP	AX
	CALL	FIPT1
	JNC	FBUF0
	MOV	AX,[TEMP]
	MOV	[STPOS],AX
	ADD	AX,[TEMP+4]
	MOV	[ENDPOS],AX
	MOV	AX,[TEMP+2]
	MOV	[STPOS+2],AX
	ADD	AX,[TEMP+6]
	MOV	[ENDPOS+2],AX
	MOV	AX,[CMDS+2]
	TEST	AX,AX
	JNZ	FBUF4
	INC	AX
FBUF4:	MOV	[STLIN],AX
	STC
	RET
FIPT:	CALL	PREPF
	JC	FIPT0
	MOV	AX,3D02H
	INT	21H
	JNC	FIPT1
	JMP	FAIL
FIPT1:	MOV	[INFILE],AX
	CALL	FFND
	PUSHF
	MOV	AH,3EH
	INT	21H
	POPF
	JC	FIPT0
	STC
	RET
FIPT0:	MOV	AH,9
	MOV	DX,OFFSET MEMERR
	INT	21H
	CLC
	RET
FFND:	MOV	AX,4200H
	XOR	CX,CX
	XOR	DX,DX
	MOV	BX,[INFILE]
	INT	21H
	MOV	DI,OFFSET TEMP
	XOR	AX,AX
	MOV	CX,4
	CLD
	MOV	SI,DI
	REPZ	STOSW
	MOV	BP,[CMDS+2]
	TEST	BP,BP
	JZ	FFND1
	CALL	FRNG
	JC	FFND3
FFND1:	ADD	SI,4
	MOV	BP,[CMDS+4]
	TEST	BP,BP
	JZ	FFND2
	SUB	BP,[CMDS+2]
	JC	FFND0
	INC	BP
	INC	BP
FFND2:	CALL	FRNG
FFND3:	MOV	DX,[TEMP+6]
	TEST	DX,DX
	JNZ	FFND0
	CALL	PEND
	XOR	DX,DX
	MOV	CX,[TEMP+4]
	CALL	RESIZE
	JC	FFND0
	PUSH	CX
	PUSH	DI
	MOV	AX,4200H
	MOV	DX,[TEMP]
	MOV	CX,[TEMP+2]
	MOV	BX,[INFILE]
	INT	21H
	POP	DX
	POP	CX
	MOV	AH,3FH
	INT	21H
	CLC
	RET
FFND0:	STC
	RET
FRNG:	MOV	DI,OFFSET INPUT
	MOV	AH,3FH
	MOV	CX,100H
	MOV	DX,DI
	MOV	BX,[INFILE]
	INT	21H
	TEST	AX,AX
	JZ	FRNG0
	MOV	CX,0A0DH
	XOR	DX,DX
	XCHG	CX,AX
FRNG1:	ADD	[SI],DX
	ADC	WORD PTR [SI+2],0
	CMP	[DI],AH
	JNZ	FRNG2
	ADD	WORD PTR [SI],1
	ADC	WORD PTR [SI+2],0
	INC	DI
	DEC	CX
	JZ	FRNG
FRNG2:	MOV	DX,DI
	CLD
	REPNZ	SCASB
	JZ	FRNG3
	SUB	DI,DX
	ADD	[SI],DI
	ADC	WORD PTR [SI+2],0
	JMP	SHORT FRNG
FRNG3:	JCXZ	FRNG4
	CMP	[DI],AH
	JNZ	FRNG4
	INC	DI
	DEC	CX
FRNG4:	NEG	DX
	ADD	DX,DI
	TEST	BP,BP
	JZ	FRNG1
	DEC	BP
	JNZ	FRNG1
FRNG5:	MOV	AX,4200H
	MOV	CX,[SI+2]
	MOV	DX,[SI]
	MOV	BX,[INFILE]
	INT	21H
	CLC
	RET
FRNG0:	STC
	RET
CMDLTR	DB	"BCDEGHILMQRSUWX"
CMDOFF	DW	FBUF
	DW	LCPY
	DW	LDEL
	DW	LEDT
	DW	FIPT
	DW	HELP
	DW	LINS
	DW	LIST
	DW	LMOV
	DW	QUIT
	DW	LREP
	DW	LFND
	DW	STAT
	DW	FSAV
	DW	FXPT
HLPT	DB	"buffer",9,9,"[start][,end]"
	DB	"B[file]",13,10
	DB	"copy",9,9,"dest,start,endC",13,10
	DB	"delete",9,9,"start,endD",13,10
	DB	"edit",9,9,"dest[E[text]]",13,10
	DB	"get",9,9,"[dest][,start][,end]"
	DB	"Gfile",13,10
	DB	"help",9,9,"H",13,10
	DB	"insert",9,9,"[dest]I[text]",13,10
	DB	"list",9,9,"[start][,end]L",13,10
	DB	"move",9,9,"dest,start,endM",13,10
	DB	"quit",9,9,"Q",13,10
	DB	"replace",9,9,"[start][,end]"
	DB	"Rstring1^Zstring2",13,10
	DB	"search",9,9,"[start][,end]"
	DB	"Sstring",13,10
	DB	"status",9,9,"U",13,10
	DB	"write",9,9,"W",13,10
	DB	"export",9,9,"[start][,end]"
	DB	"Xfile",13,10
	DB	13,10
	DB	"To input arbitrary ASCII "
	DB	"characters, use ^Z^Zcode"
	DB	13,10,36
AMSG	DB	"PC-72 REVISING PROGRAM ONE "
	DB	"1.01 FOR DOS",13,10,36
USAGE	DB	"REV1 [<IN>]",13,10,36
DOSERR	DB	"BAD DOS VERSION",13,10,36
ERROR	DB	"ERROR",13,10,36
FILERR	DB	" READING "
MEMERR	DB	"NOT ENOUGH MEMORY",13,10,36
NOFILE	DB	"NO FILE SPECIFIED",13,10,36
FORH	DB	"ENTER H FOR HELP",13,10,36
CTRLZ	DB	"^Z TO TERMINATE",13,10,36
YNC	DB	"[Y]ES [N]O [C]ANCEL"
CRLF	DB	13,10,36
LINES	DB	" LINES FROM "
BLANK	DB	"BLANK FILE"
BYTES	DB	" BYTES FREE"
PROMPT	DB	"-$"
CUR	DW	?
STLIN	DW	?
MAXMEM	DW	?
INFILE	DW	?
TMPFILE	DW	?
BUFLEN	DW	?
STPOS	DW	?,?
ENDPOS	DW	?,?
CMDS	DS	10H
INFN	DS	40H
TMPFN	DS	40H
INPUT	DS	100H
BUFFER:
