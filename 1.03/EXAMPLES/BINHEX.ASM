		CALL OPENF
		JNC MAIN
		RET
MAIN:		MOV SI, OFFSET SOFREC
		MOV BX, [OUTHDL]
		CALL FWLT
MAINL:		MOV AH, 3FH
		MOV CX, 10H
		MOV DX, OFFSET BUFFER+1
		MOV BX, [INHDL]
		INT 21H
		OR AX, AX
		JZ MAIN0
		MOV [BUFFER], AL
		MOV SI, OFFSET BUFFER
		MOV BX, [PC]
		ADD [PC], AX
		CALL IHEX
		MOV SI, OFFSET BUFFER
		MOV BX, [OUTHDL]
		CALL FWLT
		JMP SHORT MAINL
MAIN0:		MOV SI, OFFSET EOFREC
		MOV BX, [OUTHDL]
		CALL FWLT
		CALL CLOSF
		RET
OPENF:		MOV SI, 81H
		MOV DI, OFFSET INFILE
		CALL GETFN
		JZ NOPE
		MOV DI, OFFSET OUTFILE
		CALL GETFN
		JNZ GOTOUT
		MOV SI, OFFSET INFILE
		MOV DI, OFFSET OUTFILE
		MOV AL, [INFILE]
		CLD
		STOSB
		CALL WRM
		MOV DI, OFFSET OUTFILE
		STC
		CALL ADDEXT
GOTOUT:		MOV SI, OFFSET DOTCOM
		MOV DI, OFFSET INFILE
		CLC
		CALL ADDEXT
		MOV SI, OFFSET DOTHEX
		MOV DI, OFFSET OUTFILE
		CLC
		CALL ADDEXT
		MOV SI, OFFSET INFILE
		CALL PREPF
		MOV SI, OFFSET OUTFILE
		CALL PREPF
		MOV AX, 3D00H
		MOV DX, OFFSET INFILE+1
		INT 21H
		JC FAIL
		MOV [INHDL], AX
		MOV SI, OFFSET OUTFILE
		MOV AH, 3CH
		XOR CX, CX
		MOV DX, OFFSET OUTFILE+1
		INT 21H
		JNC OK
		MOV AH, 3EH
		MOV BX, [INHDL]
		INT 21H
FAIL:		PUSH SI
		MOV DI, 80H
		MOV SI, OFFSET FILERR
		CALL WRM
		POP SI
		CALL FINAL
		STC
		RET
NOPE:		MOV AH, 9
		MOV DX, OFFSET USAGE
		INT 21H
		STC
		RET
OK:		MOV [OUTHDL], AX
		MOV SI, OFFSET INM
		MOV DI, 80H
		CALL WRM
		MOV SI, OFFSET INFILE
FINAL:		CALL WRM
		MOV AX, 0A0DH
		STOSW
		MOV AL, 24H
		STOSB
		MOV AH, 9
		MOV DX, 80H
		INT 21H
		CLC
		RET
GETFN:		XOR DX, DX
		PUSH DI
		INC DI
		CLD
GETFN1:		LODSB
		CMP AL, 0DH
		JZ GETFN0
		CMP AL, 20H
		JNA GETFN2
		CMP AL, 61H
		JC GETFN3
		CMP AL, 7AH
		JA GETFN3
		AND AL, 0DFH
GETFN3:		INC DX
		STOSB
		JMP SHORT GETFN1
GETFN2:		OR DX, DX
		JZ GETFN1
GETFN0:		DEC SI
		POP DI
		MOV [DI], DL
		OR DX, DX
		RET
ADDEXT:		LAHF
		CLD
		MOV BP, DI
		XOR CH, CH
		MOV CL, [DI]
		INC DI
		MOV AL, 2EH
	REPNZ	SCASB
		JNZ ADDXT2
		SAHF
		JNC ADDXT0
		DEC DI
ADDXT2:		SAHF
		JC ADDXT1
		STOSB
		CALL WRM
ADDXT1:		MOV AX, DI
		SUB AX, BP
		DEC AX
		MOV DI, BP
		STOSB
ADDXT0:		RET
PREPF:		XOR AH, AH
		CLD
		LODSB
		ADD SI, AX
		MOV [SI], AH
		RET
CLOSF:		MOV SI, OFFSET OUTM
		MOV DI, 80H
		CALL WRM
		MOV SI, OFFSET OUTFILE
		CALL FINAL
		MOV AH, 3EH
		MOV BX, [INHDL]
		INT 21H
		MOV AH, 3EH
		MOV BX, [OUTHDL]
		INT 21H
		RET
IHEX:		PUSH SI
		CLD
		LODSB
		XOR AH, AH
		MOV CX, AX
		MOV DI, 80H
		XOR DX, DX
		MOV AL, 3AH
		STOSB
		MOV AL, CL
		ADD DL, AL
		CALL HALX
		STOSW
		MOV AL, BH
		ADD DL, AL
		CALL HALX
		STOSW
		MOV AL, BL
		ADD DL, AL
		CALL HALX
		STOSW
		XOR AL, AL
		CALL HALX
		STOSW
IHEXL:		LODSB
		ADD DL, AL
		CALL HALX
		STOSW
		LOOP IHEXL
		MOV AL, DL
		NEG AL
		CALL HALX
		STOSW
		MOV AX, 0A0DH
		STOSW
		MOV SI, 80H
		MOV CX, DI
		SUB CX, SI
		POP DI
		MOV AL, CL
		STOSB
	REPZ	MOVSB
		RET
FWLT:		PUSH AX
		PUSH CX
		PUSH DX
		PUSH BX
		MOV AH, 40H
		MOV CL, [SI]
		XOR CH, CH
		MOV DX, SI
		INC DX
		INT 21H
		POP BX
		POP DX
		POP CX
		POP AX
		RET
WRM:		MOV CL, [SI]
		XOR CH, CH
		INC SI
		CLD
	REPZ	MOVSB
		RET
HALX:		MOV AH, AL
		AND AL, 0FH
		SHR AH, 1
		SHR AH, 1
		SHR AH, 1
		SHR AH, 1
		CMP AL, 9
		JNA HALX1
		ADD AL, 7
HALX1:		XCHG AH, AL
		CMP AL, 9
		JNA HALX2
		ADD AL, 7
HALX2:		ADD AX, 3030H
		RET
VAL:		OR BX, BX
		JZ VAL9
		CMP AL, 30H
		JC VAL9
		CMP AL, 39H
		JA VAL9
		CMP BX, 1
		JA VAL1
		XOR AH, AH
		SUB AL, 30H
		INC SI
		CLC
		RET
VAL1:		CLD
		PUSH CX
		PUSH DX
		PUSH BX
		PUSH SI
		MOV CX, BX
		XOR DX, DX
		DEC BX
		MOV AL, [BX+SI]
		MOV BL, 10
		CMP AL, 48
		JC VAL0
		CMP AL, 57
		JNA VAL5
		AND AL, 0DFH
		MOV BL, 16
		DEC CX
		CMP AL, 72
		JZ VAL5
		CMP AL, 79
		JZ VAL8
		CMP AL, 81
		JZ VAL4
		CMP AL, 66
		JZ VAL2
VAL0:		XOR AX, AX
		POP SI
		POP BX
		POP DX
		POP CX
VAL9:		STC
		RET
VAL2:		SHR BX, 1
VAL4:		SHR BX, 1
VAL8:		SHR BX, 1
VAL5:		OR DX, DX
		JZ VAL6
		MOV AX, DX
		MUL BX
		MOV DX, AX
VAL6:		LODSB
		CMP AL, 48
		JC VAL0
		CMP AL, 57
		JNA VAL7
		AND AL, 0DFH
		SUB AL, 7
VAL7:		SUB AL, 48
		XOR AH, AH
		ADD DX, AX
		LOOP VAL5
		MOV AX, DX
		POP SI
		POP BX
		ADD SI, BX
		POP DX
		POP CX
		CLC
		RET
PC		DW 100H
SOFREC		DB 21, ":0400000300000100F8", 13, 10
EOFREC		DB 13, ":00000001FF", 13, 10
DOTCOM		DB 3, "COM"
DOTHEX		DB 3, "HEX"
INM		DB 4, "In: "
OUTM		DB 5, "Out: "
FILERR		DB 17, "Unable to access "
USAGE		DB "BINHEX <in>[.COM] [<out>[.HEX]]"
		DB 13, 10, 36
INHDL		DW ?
OUTHDL		DW ?
INFILE		DS 40H
OUTFILE		DS 40H
BUFFER:
